*** Settings ***
Library           SSHLibrary
Library           OperatingSystem

*** Variables ***
${karaf_shell_port}     8101
${karaf_prompt}         opendaylight-user
${karaf_user}           karaf
${karaf_password}       karaf

*** Keywords ***
Verify Feature Is Installed
    [Documentation]     Will Succeed if the given ${feature_name} is found in the output of "feature:list -i"
    [Arguments]     ${feature_name}     ${controller}=${CONTROLLER}     ${karaf_port}=${karaf_shell_port}
    ${output}=  Issue Command On Karaf Console  feature:list -i | grep ${feature_name}     ${controller}   ${karaf_port}
    Should Contain  ${output}   ${feature_name}
    [Return]    ${output}

Verify Feature Is Not Installed
    [Documentation]     Will Succeed if the given ${feature_name} is NOT found in the output of "feature:list -i"
    [Arguments]     ${feature_name}     ${controller}=${CONTROLLER}     ${karaf_port}=${karaf_shell_port}
    ${output}=  Issue Command On Karaf Console  feature:list -i | grep ${feature_name}     ${controller}   ${karaf_port}
    Should Not Contain  ${output}   ${feature_name}
    [Return]    ${output}

Issue Command On Karaf Console
    [Documentation]     Will execute the given ${cmd} by ssh'ing to the karaf console running on ${CONTROLLER}
    [Arguments]     ${cmd}     ${controller}=${CONTROLLER}     ${karaf_port}=${karaf_shell_port}
    Open Connection    ${controller}    port=${karaf_port}  prompt=${karaf_prompt}
    Login    ${karaf_user}    ${karaf_password}
    ${output}=  Execute Command     ${cmd}
    Close Connection
    Log     ${output}
    [Return]    ${output}